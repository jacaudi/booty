#!ipxe

echo Hello from Booty!

set menu-timeout 5000

###################### MAIN MENU ####################################

:start
menu iPXE boot menu for Booty
item --gap --             ------------------------- Booty ------------------------------
item --key i install       Install ublue
item --key d run-from-disk Boot from Disk
item shell                 Drop to iPXE shell
item reboot                Reboot computer
item
item --key x exit         Exit iPXE and continue BIOS boot
choose --timeout ${menu-timeout} --default ${menu-default} selected || goto cancel
set menu-timeout 0
goto ${selected}

#######

:shell
echo Type 'exit' to get the back to the menu
shell
set menu-timeout 0
set submenu-timeout 0
goto start

:failed
echo Booting failed, dropping to shell
goto shell

:reboot
reboot

:exit
exit

:install
kernel ${BASEURL}/fedora-coreos-${VERSION}-live-kernel-x86_64 initrd=main coreos.inst.install_dev=/dev/sda coreos.live.rootfs_url=${BASEURL}/fedora-coreos-${VERSION}-live-rootfs.x86_64.img ignition.firstboot ignition.platform.id=metal ignition.firstboot=1 coreos.inst.ignition_url=${CONFIGURL}
initrd --name main ${BASEURL}/fedora-coreos-${VERSION}-live-initramfs.x86_64.img
boot || goto failed
goto start

:run-from-disk
sanboot --no-describe --drive 0x80 || goto failed
goto start